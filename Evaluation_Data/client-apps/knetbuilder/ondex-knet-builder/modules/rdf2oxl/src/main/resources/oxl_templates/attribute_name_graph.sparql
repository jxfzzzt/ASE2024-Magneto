CONSTRUCT 
{
  ?attrib bk:_parentId ?parentId;
    dcterms:identifier ?id;
    rdfs:label ?label;
  	dcterms:description ?description;
  	rdfs:range ?dataType;
		bk:ondexRange ?ondexRange;  	
  	bk:_unitId ?unitId;
  	bk:_unitLabel ?unitLabel;
  	bk:_unitDescription ?unitDescription
}
WHERE
{
	VALUES ( ?attrib ) {
	  $resourceIris
	} 

  ?attrib dcterms:identifier ?id.
  
  #Â Hook to the parent, which might miss certain attributes if it's the root bk:attribute 
  { 
    ?attrib rdfs:subPropertyOf bk:attribute.
    BIND ( bk:attribute AS ?parent )
  }
  UNION { 
  	?attrib rdfs:subPropertyOf ?parent.
  	?parent bk:isOndexPreferred true 
  } 

  OPTIONAL { ?parent dcterms:identifier ?parentId }

  OPTIONAL { ?attrib rdfs:label ?label }
  OPTIONAL { ?attrib dcterms:description ?description }
  
  OPTIONAL { ?attrib rdfs:range ?dataType } 
  OPTIONAL { ?attrib bk:ondexRange ?ondexRange }
       
  # Unit
  #
  OPTIONAL {
  	?attrib bk:attributeUnit ?unit.
  	?unit dcterms:identifier ?unitId.

  	OPTIONAL { ?unit rdfs:label ?unitLabel }
	  OPTIONAL { ?unit dcterms:description ?unitDescription }
  }
}

