SELECT 
	(COUNT ( DISTINCT ?concept ) AS ?conceptsCount ) 
	(COUNT ( DISTINCT ?relation ) AS ?relationsCount)
WHERE
{
	# Matching concepts
  { 
  	?concept a/rdfs:subClassOf* bk:Concept. 

  	# These are foo test cases in the ontology file.
  	FILTER ( ?concept NOT IN ( bk:testConcept0_1, bk:testConcept0_2 ) )
  }
  
  UNION
  
  # Matching relations
  {
    # The subquery is to optimise it
    {SELECT DISTINCT ?relationProp { ?relationProp rdfs:subPropertyOf+ bk:conceptsRelation. }}

    ?c1 ?relationProp ?c2.
  	
  	OPTIONAL {
			?reifiedRel 
				bk:relFrom ?c1;
				bk:relTypeRef ?relationProp;
				bk:relTo ?c2;
  	}
  	BIND ( 
  		IF ( BOUND ( ?reifiedRel ), 
  	  		?reifiedRel, 
        	CONCAT ( STR ( bkr: ), MD5 ( CONCAT ( STR ( ?relationProp ), STR ( ?c1 ), STR ( ?c2 ) ) ) )
      	)
  	  AS ?relation
  	)
  }
}