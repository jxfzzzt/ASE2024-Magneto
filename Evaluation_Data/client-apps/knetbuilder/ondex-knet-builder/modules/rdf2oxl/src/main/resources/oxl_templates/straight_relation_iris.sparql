SELECT ?from ?_typeId ?to
{
  # BEWARE that the following is optimised, it might become significantly slower if you change order/structure
  # 
 	
  # Use a subquery to get them once for all
  { 
  	SELECT DISTINCT ?type ?_typeId 
  	{ 
  		?type rdfs:subPropertyOf* bk:conceptsRelation;
  					bk:isOndexPreferred true;
  				  dcterms:identifier ?_typeId;
  	}
  }
  		
  # And then pick up the triples based on it 
  ?from ?type ?to.
    
  # This is taken care of by the reified relation queries (reified relations always have a direct statement too).
  MINUS {
    ?reifiedRel 
    	bk:relFrom ?from;
    	bk:relTo ?to;
    	bk:relTypeRef ?type.
  }
}